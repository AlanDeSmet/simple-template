#! /usr/bin/perl -w
use strict;

use Getopt::Long;
use File::Basename;
use File::Spec;
use FindBin;
use lib "$FindBin::Bin/lib";
use Text::Template;


# simple-template - A simple templating system.
#
# Copyright 2014 Alan De Smet
#
# This is free software; you can redistribute it and/or modify it under the
# terms of either:
#
# a) the GNU General Public License as published by the Free Software
# Foundation; either version 2, or (at your option) any later version, or
#
# b) The "Artistic License 2.0" as published by The Perl Foundation.
#
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License or Artistic
# License for more details.

my $extension = '.src';

my $template_file = 'template.html';

my $dst_dir = 'output';

my $template = Text::Template->new(TYPE=>'FILE', SOURCE=>$template_file);

my $verbose = 1;

exit main();

sub main {
	GetOptions(
		'template=s' => \$template_file,
		'dst=s' => \$dst_dir,
		'help' => sub {help_and_exit();},
		);
	if(@ARGV == 0) {
		print STDERR "Nothing to do\n" if $verbose;
		exit 0;
	}
	foreach my $src_file (@ARGV) {
		transform_file($src_file, $template);
	}
	return 0;
}

my $package_count;
BEGIN {
	$package_count = 0;
};

sub transform_file
{
	my($src_file, $template) = @_;
	my($src_basename) = my_basename($src_file);

	my $extension_re = quotemeta($extension);
	my($dst_basename) = ($src_basename =~ /^(.+)$extension_re$/);
	if(not defined $dst_basename) {
		die "ERROR: $src_file didn't end with $extension\n";
	}

	my $dst_file = "$dst_dir/$dst_basename";

	print "$src_file -> $dst_file\n" if $verbose;

	my $contents = slurp_or_die($src_file, 'template');
	my $data = extract_data($contents) || "";

	$package_count++;
	my $package_name = "TEMP$package_count";

	my $src = "package $package_name; no strict; do \"$src_file\";";
	my $success = eval($src);
	if(not defined $success and $@ ne '') {
		die "ERROR: failed to read $src_file: $@\n";
	};

	{
		no strict 'refs';
		${$package_name.'::DATA'} = $data;
		${$package_name.'::SRC_FILE'} = $src_file;
		${$package_name.'::DST_FILE'} = $dst_file;
		${$package_name.'::BASENAME'} = $dst_basename;
	}

	my $text = $template->fill_in(PACKAGE=>$package_name);
	open my $out, '>', $dst_file
		or die "ERROR: Unable to open $dst_file for writing: $!\n";
	print $out $text;
	close $out;
}

sub slurp_or_die {
	my($file, $desc) = @_;
	open my $IN, '<', $file or die "ERROR: Unable to read $desc file $file: $!\n";
	local $/;
	my $ret = <$IN>;
	close $IN;
	return $ret;
}

sub extract_data {
	my($src) = @_;
	my($data) = ($src =~ /[\n\r]+__DATA__[\n\r]+(.*)/s);
	return $data;
}

sub my_basename {
	my($path) = @_;
	my($volume, $directory, $basename) = File::Spec->splitpath($path);
	return $basename;
}

sub help_and_exit {
	my($error) = @_;
	print <<END;
Usage: $0 <options> file.src [file.src] ...
Transforms input files using a template and writes output to output directory.
Options:
   --template=FILENAME  Template to use. Default: $template_file
   --dst=DIRNAME        Directory to write output to. Default: $dst_dir
END
	if(defined $error) { print "$error\n"; exit 1;}
	exit 0;
}
